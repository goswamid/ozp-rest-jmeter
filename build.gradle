buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
  dependencies {
    classpath 'org.akhikhl.gretty:gretty:1.1.4'
    classpath 'com.github.kulya:jmeter-gradle-plugin:1.3.3-2.10-SNAPSHOT'
  }
}

import com.github.kulya.gradle.plugins.jmeter.JmeterRunTask
import com.github.kulya.gradle.plugins.jmeter.JmeterRunGuiTask

apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'jmeter'

repositories {
  mavenLocal()
  jcenter()
}

gretty {
  jvmArgs = [
    '-Xms256m',
    '-Xmx1536m',
    '-XX:MaxPermSize=256m'
  ]
}

farm {
  servletContainer = 'tomcat7'
  webapp 'org.ozoneplatform:marketplace:0.1.0', contextPath: '/marketplace'

  afterEvaluate {
    tasks.farmIntegrationTest.dependsOn tasks.jmeterRun
    //tasks.test.dependsOn tasks.farmIntegrationTest
  }
}

def propsFile = file 'jmeter.properties'
def apiResources = [
  'Agency',
  'API',
  'ApplicationLibrary',
  'Category',
  'ContactType',
  'Intent',
  'ListingActivity',
  'ListingComment',
  'ListingRelationships',
  'Profile',
  'ProfileOrganizationStewards',
  'Scorecard',
  'Type'
]

apiResources.each { resource ->
  def testFile = file "test/${resource}/${resource}.jmx"

  task "test$resource"(type: JmeterRunTask) {
    jmeterTestFiles = [testFile]
    jmeterPropertyFile = propsFile 
    maxHeapSize = '512m'
    enableReports = true
    reportDir = file "$project.buildDir/jmeter-report"
    reportPostfix = '-report.html'
    remote = false
  }

  tasks.test.dependsOn tasks["test$resource"]

  task "edit$resource"(type: JmeterRunGuiTask) {
    jmeterEditFile = testFile
    jmeterPropertyFile = propsFile
    maxHeapSize = '512m'
  }
}

jmeterRun {
    jmeterTestFiles = apiResources.collect { resource ->
        file "test/${resource}/${resource}.jmx"
    }

    jmeterPropertyFile = propsFile
}

jmeterEditor {
    jmeterPropertyFile = propsFile
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.1'
}

task list << {
  apiResources.each { resource ->
    println resource
  }
}