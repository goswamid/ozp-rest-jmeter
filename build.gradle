buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.akhikhl.gretty:gretty:1.1.4'
    classpath 'com.github.kulya:jmeter-gradle-plugin:1.3.2-2.9'
  }
}

import org.akhikhl.gretty.FarmStartTask

apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'jmeter'

repositories {
  mavenLocal()
  jcenter()
}

gretty {
  jvmArgs = [
    '-Xms256m',
    '-Xmx1536m',
    '-XX:MaxPermSize=256m'
  ]
}

farm {
  servletContainer = 'tomcat7'
  webapp 'org.ozoneplatform:marketplace:0.1.0', contextPath: '/marketplace'

  afterEvaluate {
    tasks.farmIntegrationTest.dependsOn tasks.jmeterRun
    tasks.test.dependsOn tasks.farmIntegrationTest
  }
}

jmeterRun {
    jmeterTestFiles = [
        'API/API.jmx',
        'Category/Category.jmx',
        'Agency/Agency.jmx',
        'ApplicationLibrary/ApplicationLibrary.jmx'
    ].collect { path ->
        file "test/$path"
    }

    jmeterPropertyFile = file 'jmeter.properties'
}

jmeterEditor {
    jmeterPropertyFile = file 'jmeter.properties'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.1'
}